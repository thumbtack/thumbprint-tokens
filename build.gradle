buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.41'
    }
}

plugins {
    id 'maven-publish'
    id 'com.moowork.node' version '1.3.1'
}

apply plugin: 'com.android.library'

archivesBaseName = "thumbprint-tokens"

node {
    nodeModulesDir = file("${project.projectDir}")
    download = true
}

allprojects {
  repositories {
      google()
      jcenter()
  }
}

android {
    compileSdkVersion 28
    buildToolsVersion '29.0.1'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    sourceSets {
        main.res.srcDirs = ["${projectDir}/dist/android/index.xml"]
        main.manifest.srcFile "${projectDir}/AndroidManifest.xml"
    }
}

task buildThumbprintDistFiles(type: YarnTask) {
  args = ['run', 'build']
}

buildThumbprintDistFiles.dependsOn 'yarn'
preBuild.dependsOn 'buildThumbprintDistFiles'

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.github.thumbtack'
            artifact("${buildDir}/outputs/aar/thumbprint-tokens-release.aar")
        }
    }
}
